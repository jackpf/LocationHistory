plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.protobuf)
}

android {
    namespace 'com.jackpf.locationhistory.client'
    compileSdk {
        version = release(36)
    }

    defaultConfig {
        applicationId "com.jackpf.locationhistory.client"
        minSdk 22
        targetSdk 36
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile = file(System.getenv("KEYSTORE_PATH") ?: "debug.keystore")
            storePassword = System.getenv("KEYSTORE_PASSWORD")
            keyAlias = System.getenv("KEY_ALIAS")
            keyPassword = System.getenv("KEY_PASSWORD") ?: System.getenv("KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
        viewBinding true
    }

    packaging {
        resources {
            // Fix netty conflicts
            pickFirst 'META-INF/io.netty.versions.properties'
            pickFirst 'META-INF/INDEX.LIST'
            pickFirst 'META-INF/DEPENDENCIES'
        }
    }

    sourceSets {
        main {
            proto {
                srcDir '../../shared/src/main/protobuf'
            }
        }
    }
}

protobuf {
    protoc {
        artifact = libs.protobuf.protoc.get().toString()
    }
    plugins {
        grpc {
            artifact = libs.grpc.gen.java.get().toString()
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
            task.plugins {
                grpc {
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.activity.compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.compose.ui
    implementation libs.androidx.compose.ui.graphics
    implementation libs.androidx.compose.ui.tooling.preview
    implementation libs.androidx.compose.material3
    implementation libs.protobuf.javalite
    implementation libs.grpc.protobuf.lite
    implementation libs.grpc.stub
    implementation libs.grpc
    implementation libs.javax.annotation.api
    implementation libs.guava
    implementation libs.work
    implementation libs.concurrent
    implementation libs.fragment
    implementation libs.appcompat
    implementation libs.unifiedpush
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.compose.ui.test.junit4
    debugImplementation libs.androidx.compose.ui.tooling
    debugImplementation libs.androidx.compose.ui.test.manifest
    compileOnly libs.lombok
    annotationProcessor libs.lombok
    coreLibraryDesugaring libs.desugar.jdk.libs
}

configurations.configureEach {
    exclude group: 'com.google.protobuf', module: 'protobuf-java'
}